#!/usr/bin/env zsh
function chmoujump() {
    local jumpdirs=${CHMOUJUMP_DIRS:-"$GOPATH/src/github.com"}
    [[ -d ${jumpdirs} ]] || jumpdirs=$HOME
    local opt_n opt_p opt fnd
    local res args preview
    local notfuzzy="-e"
    local sedcmd=sed;type -p gsed >/dev/null 2>/dev/null && sedcmd=gsed # osx brew sed detection
    local fdcmd=fd;type -p fdfind >/dev/null 2>/dev/null && fdcmd=fdfind # linux package
    local reverser=tac
    if [[ $OSTYPE == darwin* && -z $(whence tac) ]]; then
        reverser='tail -r'
    fi
    local zshz_listing="{ zshz -l | ${reverser} | sed 's/.* //' }"


    if ! (( ${+CHMOUJUMP_PREVIEW_COMMAND} )); then
        command -v exa >/dev/null 2>&1
        if [ $? -eq 0 ]; then
            CHMOUJUMP_PREVIEW_COMMAND="exa --level 2 --tree --color=always --group-directories-first {}"
        else
            command -v tree >/dev/null 2>&1
            if [ $? -eq 0 ]; then
                CHMOUJUMP_PREVIEW_COMMAND="tree -C -L 2 -x --noreport --dirsfirst {}"
            else
                CHMOUJUMP_PREVIEW_COMMAND="ls -1 -R {}"
            fi
        fi
    fi

    while getopts ":npzfP" opt;do
        case $opt in
            f) notfuzzy=;;
            z) opt_zle=true;;
            n) opt_n=true;;
            p) opt_p=true;;
            P) preview="--preview=\"${CHMOUJUMP_PREVIEW_COMMAND}\"";;
            \*) print nothing: $OPTARG; usage;;
            \?) print invalid option: $OPTARG; usage;;
        esac
    done
    (( OPTIND > 1 )) && shift $(( OPTIND - 1 ))

    (( $+functions[zshz] )) || {
        opt_n=true
        zshz_listing="{ true }"
    }

    fnd=$1
    if [[ -n ${fnd} ]];then
	    [[ -z ${opt_n} ]] && {
		    local zz=$(zshz -e ${fnd})
		    [[ -n ${zz} ]] && {
			    echo "ZSHZ: ${zz}"
			    [[ -z ${opt_p} ]] && cd ${zz}
			    return
		    }
	    }
	    args="-q ${fnd}"
    fi

    local fdlisting="{ ${fdcmd} -d 2 -t d . ${=jumpdirs} }"
    local sorter="uniq -u"
    # --preview=\"${CHMOUJUMP_PREVIEW_COMMAND}\"
    local fzfcmd="fzf ${preview} ${notfuzzy} -1 --no-sort --tiebreak=end,index --header=\"Choose a directory to jump to:\" --min-height=10 --height 30%  ${args}"

    cmd="res=\$({ ${zshz_listing} ; ${fdlisting} } | ${sorter} | ${fzfcmd})"
    eval ${cmd}

    [[ -n ${res} ]] && {
	    echo ${res}
	    [[ -z ${opt_p} ]] && cd ${res}
    }
}
# vim: ft=zsh
