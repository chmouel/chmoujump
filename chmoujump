#!/usr/bin/env zsh
function chmoujump() {
    local jumpdirs=${CHMOUJUMP_DIRS:-"$GOPATH/src/github.com"}
    [[ -d ${jumpdirs} ]] || jumpdirs=$HOME
    local opt_n opt_p opt fnd listalias choosealias
    local res args preview
    local notfuzzy="-e"
    local sedcmd=sed;type -p gsed >/dev/null 2>/dev/null && sedcmd=gsed # osx brew sed detection
    local fdcmd=fd;type -p fdfind >/dev/null 2>/dev/null && fdcmd=fdfind # linux package
    local reverser=tac
    if [[ $OSTYPE == darwin* && -z $(whence tac) ]]; then
        reverser='tail -r'
    fi
    local zshz_listing="{ zshz -l | ${reverser} | sed 's/.* //' }"


    command -v exa >/dev/null 2>&1
    if [ $? -eq 0 ]; then
        preview_command="exa --level 2 --tree --color=always --group-directories-first {}"
    else
        command -v tree >/dev/null 2>&1
        if [ $? -eq 0 ]; then
            preview_command="tree -C -L 2 -x --noreport --dirsfirst {}"
        else
            preview_command="ls -1 -R {}"
        fi
    fi

    while getopts ":npfPAa" opt;do
        case $opt in
            a) choosealias=true ;;
            A) listalias=true ;;
            f) notfuzzy= ;;
            n) opt_n=true ;;
            p) opt_p=true ;;
            P) preview="--preview=\"${preview_command}\"" ;;
            \*) print nothing: $OPTARG; return ;;
            \?) print invalid option: $OPTARG; return ;;
        esac
    done
    (( OPTIND > 1 )) && shift $(( OPTIND - 1 ))

    (( $+functions[zshz] )) || {
        opt_n=true
        zshz_listing="{ true }"
    }

    [[ ${listalias} == true ]] && {
        zmodload zsh/stat
        for ((i=1; i<$#CHMOUJUMP_ALIASES; i++)); do times[$i]=$(stat +ctime -L -- \
            ${${(@v)CHMOUJUMP_ALIASES}[$i]}/.git/index):${${(@v)CHMOUJUMP_ALIASES}[$i]/.git/index}; done
        print -l ${${${(o)times}#*:}/$HOME\//\~/}
        return
    }

    [[ ${choosealias} == true ]] && {
        choosealias=$(chmoujump -A|fzf --tac)
        choosealias=${choosealias##* }
        [[ ${choosealias} =~ ^\~ ]] && choosealias=${choosealias/#\~/${HOME}}
        [[ -z ${opt_p} ]] && cd ${choosealias}
        return
    }

    fnd=$1
    if [[ -n ${fnd} ]];then
        if (( ${+CHMOUJUMP_ALIASES[$fnd]} ));then
            echo "ALIAS: ${CHMOUJUMP_ALIASES[$fnd]}"
            [[ -z ${opt_p} ]] && {
                cd ${CHMOUJUMP_ALIASES[$fnd]}
            }
            return
        fi

        [[ -z ${opt_n} ]] && {
            local zz=$(zshz -e ${fnd})
            [[ -n ${zz} ]] && {
                echo "ZSHZ: ${zz}"
                [[ -z ${opt_p} ]] && {
                    [[ ${zz} =~ ^\~ ]] && zz=${zz/#\~/${HOME}}
                    cd ${zz}
                }
                return
            }
        }
        args="-q ${fnd}"
    fi

    local fdlisting="{ ${fdcmd} -d 2 -t d . ${=jumpdirs} }"
    local sorter="sed 's,$HOME,\~,'|uniq"
    # --preview=\"${preview_command}\"
    local fzfcmd="fzf ${preview} ${notfuzzy} -1 --no-sort --tiebreak=end,index --header=\"Choose a directory to jump to:\" ${args}"

    cmd="res=\$({ ${zshz_listing} ; ${fdlisting} } | ${sorter} | ${fzfcmd})"
    eval ${cmd}

    [[ -n ${res} ]] && {
        [[ ${res} =~ ^\~ ]] && res=${res/#\~/${HOME}}
        echo ${res}
        [[ -z ${opt_p} ]] && {
            cd ${res}
        }
    }
}
# vim: ft=zsh
